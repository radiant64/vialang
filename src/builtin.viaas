;-------------------------------------------------------------------------------
eval-proc:
;-------------------------------------------------------------------------------
    load !expr

    ; Check if the value is a frame, if true assume the frame.
    push
    framep
    skipz @no-frame-assume
        pop
        call assume-proc
@no-frame-assume:
    pop

    ; Check if the value is a compound expression; if true return the result of
    ; a compound evaluation, replacing the current frame.
    push
    pairp
    skipz @no-eval-compound
        pop
        call eval-compound-proc
@no-eval-compound:
    pop

    ; Check if the value is a builtin; if true return the result of executing
    ; it, replacing the current frame.
    push
    builtinp
    skipz @no-builtin-exec
        pop
        callacc
@no-builtin-exec:
    pop
    
    ; Check if the value is a a symbol; if true return the lookup result,
    ; replacing the current frame.
    push
    symbolp
    skipz @no-symbol-lookup
        pop
        call lookup-proc
@no-symbol-lookup:
    pop

    ; Return the literal result.
    setret
    return


;-------------------------------------------------------------------------------
eval-compound-proc:
;-------------------------------------------------------------------------------
    load !expr
    car

    ; Check if the compound expression is a special form, Special forms are
    ; identified by their head value, which must be a symbol literal that
    ; resolves to a form.
    push
    symbolp
    skipz @skip-special-form-check
        pop
        snap 1
            call lookup-proc
        loadret

        ; If the symbol resolved to a special form, set the rest of the current
        ; expression as !ctxt, and evaluate the form expression, replacing the
        ; current frame.
        formp
        skipz @no-special-form
            load !expr
            cdr
            set !ctxt
            loadret
            car
            set !expr
            call eval-proc
    @no-special-form:
        loadret
        jmp @regular-form
@skip-special-form-check:
    pop

    ; Regular form; procedure application.
@regular-form:
    ; Start out by evaluating the procedure value.
    snap @proc-eval-done
        set !expr
        call eval-proc
@proc-eval-done:
    ; Set the !proc register to the result, clear !args, and load the original
    ; expression in the accumulator.
    loadret
    set !proc
    loadnil
    set !args
    load !expr

    ; Iterate over the rest of the expression and evaluate the terms as
    ; arguments.
@arg-eval-loop:
    cdr
    set !expr
    skipz @no-more-args
        snap @arg-eval-done
            car
            set !expr
            call eval-proc
    @arg-eval-done:
        loadret
        pusharg
        load !expr
        jmp @arg-eval-loop

@no-more-args:
    call apply-proc


;-------------------------------------------------------------------------------
begin-proc:
;-------------------------------------------------------------------------------
    load !ctxt
    set !expr
@begin-eval-loop:
    cdr
    skipz @begin-last-expression
        snap @begin-expr-eval-done
            load !expr
            car
            set !expr
            call eval-proc
    @begin-expr-eval-done:
        load !expr
        cdr
        set !expr
        jmp @begin-eval-loop

@begin-last-expression:
    ; Replace the current frame.
    load !expr
    car
    set !expr
    call eval-proc


;-------------------------------------------------------------------------------
set-proc:
;-------------------------------------------------------------------------------
    load !ctxt
    cdr
    car
    snap @set-value-eval-done
        set !expr
        call eval-proc
@set-value-eval-done:
    loadret
    pusharg
    load !ctxt
    car
    push
    call env-set-proc
    return


;-------------------------------------------------------------------------------
if-proc:
;-------------------------------------------------------------------------------
    load !ctxt
    car
    snap @if-predicate-eval-done
        set !expr
        call eval-proc
@if-predicate-eval-done:
    loadret
    skipz @if-eval-else-clause
        load !ctxt
        cdr
        car
        set !expr
        call eval-proc
@if-eval-else-clause:
    load !ctxt
    cdr
    cdr
    skipz @if-no-else-clause
        car
        set !expr
        call eval-proc
@if-no-else-clause:
    return


;-------------------------------------------------------------------------------
throw-eval-proc:
;-------------------------------------------------------------------------------
    load !ctxt
    car
    snap @throw-eval-expr-done
        set !expr
        call eval-proc
@throw-eval-expr-done:
    loadret
    push
    call throw-proc

